plugins {
	id 'com.zyxist.chainsaw' version '0.3.1'
}

allprojects { 
	apply plugin: (it.name == 'system-test' ? 'application' : 'java-library')
	apply plugin: 'com.zyxist.chainsaw'
	apply plugin: 'eclipse'
	apply plugin: 'idea'
	
	eclipse {
		classpath {
			file.whenMerged {
				entries.findAll { 
					it.kind == 'lib' || it.kind == 'src' 
				}.each { 
					it.entryAttributes['module'] = 'true'
				}
			}
		}
	}
	
	repositories {
		mavenCentral()
	}
	
	test {
		useJUnitPlatform()
	}
	
	dependencies {
		testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.2.0'
		testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.2.0'
	}
}

apply from: 'build-scripts/eclipseIncludeGenerated.gradle'

dependencies {
	api project(':annotation')
	annotationProcessor project(':processor')
	testAnnotationProcessor project(':processor')
}

javaModule.hacks {
	//Used for testing purposes
	exports('brownshome.netcode', 'brownshome.netcode.ordering', 'org.junit.platform.commons')
	opens('brownshome.netcode', 'brownshome.netcode.ordering', 'org.junit.platform.commons')
}